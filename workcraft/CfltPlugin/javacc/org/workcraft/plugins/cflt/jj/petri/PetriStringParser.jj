options {
    JDK_VERSION = "1.8";
    STATIC = false;
    UNICODE_INPUT = true;
}

PARSER_BEGIN(PetriStringParser)
package org.workcraft.plugins.cflt.jj.petri;

import java.io.*;
import java.nio.charset.StandardCharsets;
import org.workcraft.plugins.cflt.tools.CotreeTool;
import org.workcraft.plugins.cflt.*;

public class PetriStringParser {
    CotreeTool cotreeTool;
    public void parse(String s) throws ParseException {
       cotreeTool = new CotreeTool();
       cotreeTool.reset();
       InputStream is = new ByteArrayInputStream(s.getBytes(StandardCharsets.UTF_8));
       PetriStringParser parser = new PetriStringParser(is);
       parser.parseOneLine();
    }
}
PARSER_END(PetriStringParser)

<*> SKIP:
{
    < " " | "\t" | "\r" | "\n" | "\r\n" >
|
    <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")?>
}

TOKEN:
{
    <TRANSITION: ["A"-"Z", "a"-"z", "_"] (["A"-"Z", "a"-"z", "_", "0"-"9"])*>
}

void parseOneLine():
{
    String a;
}
{
    [
        a = concurrency()
        {
            if (cotreeTool.nodes.size() == 0) {
                cotreeTool.singleTransition = a;
            }
        }
    ]
    <EOF>
}

String concurrency():
{
    String a;
    String b;
}
{
    a = choice()
    (
        "|" b = concurrency()
        {
            Node node = new Node(a, b, Operator.CONCURRENCY);
            cotreeTool.nodes.add(node);
        }
    )*
    {
        return a;
    }
}

String choice():
{
    String a;
    String b;
}
{
    a = sequence()
    (
        "#" b = choice()
        {
        	Node node = new Node(a, b, Operator.CHOICE);
        	cotreeTool.nodes.add(node);
        }	
    )*
    {
        return a;
    }
}

String sequence():
{
    String a;
    String b;
}
{
    a = unary()
    (
        [";"] b = sequence()
        {
            Node node = new Node(a, b, Operator.SEQUENCE);
            cotreeTool.nodes.add(node);
        }
    )*
    {
        return a;
    }
}

String unary():
{
    String a;
}
{
    a = element()
    {
        return a;
    }
}

String element():
{
    Token t;
    String a;
}
{
    t = <TRANSITION>
    {
        return t.toString();
    }
|
    "(" a = concurrency() ")"
    {
        return a;
    }
|
    "{" a = concurrency() "}"
    {
        cotreeTool.containsIteration = true;
        Node node = new Node(a, a, Operator.ITERATION);
        cotreeTool.nodes.add(node); return a;
    }
}
